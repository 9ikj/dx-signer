plugins {
    id 'java'
    id 'idea'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    jcenter() // for payload_writer
}

dependencies {
    implementation fileTree(dir: 'lib', include: ['*.jar'])
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.meituan.android.walle:payload_writer:1.1.7'
    implementation 'org.json:json:20230618'
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation 'com.intellij:forms_rt:7.0.3'
    implementation 'pxb.android:axml:2.0.0'

}

tasks.register('fatJar', Jar) {
    dependsOn jar
    archiveBaseName = 'dx-signer'

    def deps = configurations.runtimeClasspath

    def depClasses = { deps.collect { it.isDirectory() ? it : zipTree(it) } }

    from(depClasses) {
        exclude 'META-INF/*.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.RSA'
        exclude 'META-INF/*.DSA'
        exclude '**/*.html'
    }

    manifest {
        attributes 'Implementation-Title': 'dx signer',
                'Main-Class': 'dx.signer.UX'
    }

    from(sourceSets.main.output)
}
